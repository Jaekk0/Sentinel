let dt_lookBack = 1h; // Look back 1 hour for DeviceNetworkEvents events
let ioc_lookBack = 14d; // Look back 14 days for threat intelligence indicators
// Fetch threat intelligence indicators related to IP addresses
let IP_Indicators = ThreatIntelligenceIndicator
  | where TimeGenerated >= ago(ioc_lookBack)
  | summarize LatestIndicatorTime = arg_max(TimeGenerated, *) by IndicatorId
  // Indicates whether indicator is active and ExpirationDateTime is in the future.
  | where Active == true and ExpirationDateTime > now() 
  // Filter out indicators without relevant IP address fields
  | where isnotempty(NetworkIP) or isnotempty(EmailSourceIpAddress) or isnotempty(NetworkDestinationIP) or isnotempty(NetworkSourceIP)
  // Select the IP entity based on availability of different IP fields
  | extend TI_ipEntity = iff(isnotempty(NetworkIP), NetworkIP, NetworkDestinationIP)
  | extend TI_ipEntity = iff(isempty(TI_ipEntity) and isnotempty(NetworkSourceIP), NetworkSourceIP, TI_ipEntity)
  | extend TI_ipEntity = iff(isempty(TI_ipEntity) and isnotempty(EmailSourceIpAddress), EmailSourceIpAddress, TI_ipEntity)
  | where ipv4_is_private(TI_ipEntity) == false and  TI_ipEntity !startswith "fe80" and TI_ipEntity !startswith "::" and TI_ipEntity !startswith "127.";
// Perform a join between IP indicators and DeviceNetworkEvents events
IP_Indicators
  // Use innerunique to keep performance fast and result set low, as we only need one match to indicate potential malicious activity that needs investigation
  | join kind=innerunique (
      DeviceNetworkEvents
      | where TimeGenerated >= ago(dt_lookBack)
      | where isnotempty(RemoteIP)
      | extend DeviceNetworkEvents_TimeGenerated = TimeGenerated
  )
  on $left.TI_ipEntity == $right.RemoteIP
  // Filter out DeviceNetworkEvents events that occurred after the expiration of the corresponding indicator
  | where DeviceNetworkEvents_TimeGenerated < ExpirationDateTime
  // Group the results by IndicatorId and keep the DeviceNetworkEvents event with the latest timestamp
  | summarize DeviceNetworkEvents_TimeGenerated = arg_max(DeviceNetworkEvents_TimeGenerated, *) by IndicatorId
  // Select the desired output fields
  | project DeviceNetworkEvents_TimeGenerated, DeviceName, LocalIP, InitiatingProcessAccountDomain, InitiatingProcessAccountUpn, InitiatingProcessCommandLine, InitiatingProcessFolderPath, Protocol, Url, ExpirationDateTime, ConfidenceScore,
    TI_ipEntity, RemoteIP, NetworkIP, NetworkDestinationIP, NetworkSourceIP, EmailSourceIpAddress, Type
